From 0000000000000000000000000000000000000000 Mon Sep 17 00:00:00 2001
From: CUB3D <callumthom11@gmail.com>
Date: Sat, 15 Feb 2025 21:21:36 +0000
Subject: [PATCH] 7511: Add Xilinx PicoBlaze KCPSM3 and KCPSM6 processors

Add PicoBlaze

Add Picoblaze6
---
 Ghidra/Processors/PicoBlaze/Module.manifest   |   0
 Ghidra/Processors/PicoBlaze/README.md         |   1 +
 Ghidra/Processors/PicoBlaze/build.gradle      |  20 +
 .../PicoBlaze/certification.manifest          |  12 +
 .../PicoBlaze/data/buildLanguage.xml          |  50 +++
 .../PicoBlaze/data/languages/KCPSM3.cspec     |  17 +
 .../PicoBlaze/data/languages/KCPSM3.pspec     |   9 +
 .../PicoBlaze/data/languages/KCPSM3.slaspec   | 374 ++++++++++++++++
 .../PicoBlaze/data/languages/KCPSM6.pspec     |   9 +
 .../PicoBlaze/data/languages/KCPSM6.slaspec   | 400 ++++++++++++++++++
 .../PicoBlaze/data/languages/picoblaze.ldefs  |  30 ++
 .../data/languages/picoblaze_common.sinc      |  21 +
 .../Processors/PicoBlaze/data/sleighArgs.txt  |   6 +
 13 files changed, 949 insertions(+)
 create mode 100644 Ghidra/Processors/PicoBlaze/Module.manifest
 create mode 100644 Ghidra/Processors/PicoBlaze/README.md
 create mode 100644 Ghidra/Processors/PicoBlaze/build.gradle
 create mode 100644 Ghidra/Processors/PicoBlaze/certification.manifest
 create mode 100644 Ghidra/Processors/PicoBlaze/data/buildLanguage.xml
 create mode 100644 Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.cspec
 create mode 100644 Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.pspec
 create mode 100644 Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.slaspec
 create mode 100644 Ghidra/Processors/PicoBlaze/data/languages/KCPSM6.pspec
 create mode 100644 Ghidra/Processors/PicoBlaze/data/languages/KCPSM6.slaspec
 create mode 100644 Ghidra/Processors/PicoBlaze/data/languages/picoblaze.ldefs
 create mode 100644 Ghidra/Processors/PicoBlaze/data/languages/picoblaze_common.sinc
 create mode 100644 Ghidra/Processors/PicoBlaze/data/sleighArgs.txt

diff --git a/Ghidra/Processors/PicoBlaze/Module.manifest b/Ghidra/Processors/PicoBlaze/Module.manifest
new file mode 100644
index 0000000000..e69de29bb2
diff --git a/Ghidra/Processors/PicoBlaze/README.md b/Ghidra/Processors/PicoBlaze/README.md
new file mode 100644
index 0000000000..55d8a320d6
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/README.md
@@ -0,0 +1 @@
+# PicoBlaze
diff --git a/Ghidra/Processors/PicoBlaze/build.gradle b/Ghidra/Processors/PicoBlaze/build.gradle
new file mode 100644
index 0000000000..66c190bf6b
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/build.gradle
@@ -0,0 +1,20 @@
+/* ###
+ * IP: GHIDRA
+ *
+ * Licensed under the Apache License, Version 2.0 (the "License");
+ * you may not use this file except in compliance with the License.
+ * You may obtain a copy of the License at
+ *
+ *      http://www.apache.org/licenses/LICENSE-2.0
+ *
+ * Unless required by applicable law or agreed to in writing, software
+ * distributed under the License is distributed on an "AS IS" BASIS,
+ * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
+ * See the License for the specific language governing permissions and
+ * limitations under the License.
+ */
+apply from: "$rootProject.projectDir/gradle/distributableGhidraModule.gradle"
+apply from: "$rootProject.projectDir/gradle/processorProject.gradle"
+apply plugin: 'eclipse'
+
+eclipse.project.name = 'Processors PicoBlaze'
diff --git a/Ghidra/Processors/PicoBlaze/certification.manifest b/Ghidra/Processors/PicoBlaze/certification.manifest
new file mode 100644
index 0000000000..d58d9e5b77
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/certification.manifest
@@ -0,0 +1,12 @@
+data/buildLanguage.xml||GHIDRA||||END|
+data/languages/KCPSM3.cspec||GHIDRA||||END|
+data/languages/KCPSM3.pspec||GHIDRA||||END|
+data/languages/KCPSM3.slaspec||GHIDRA||||END
+data/languages/KCPSM6.cspec||GHIDRA||||END|
+data/languages/KCPSM6.pspec||GHIDRA||||END|
+data/languages/KCPSM6.slaspec||GHIDRA||||END|
+data/languages/picoblaze.ldefs||GHIDRA||||END|
+data/languages/picoblaze_common.sinc||GHIDRA||||END|
+data/sleighArgs.txt||GHIDRA||||END|
+Module.manifest||GHIDRA||||END|
+README.md||GHIDRA||||END|
diff --git a/Ghidra/Processors/PicoBlaze/data/buildLanguage.xml b/Ghidra/Processors/PicoBlaze/data/buildLanguage.xml
new file mode 100644
index 0000000000..74a29faf79
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/data/buildLanguage.xml
@@ -0,0 +1,50 @@
+<?xml version="1.0" encoding="UTF-8"?>
+
+<!--
+  + Compile sleigh languages within this module.
+  + Sleigh compiler options are read from the sleighArgs.txt file.
+  + Eclipse: right-click on this file and choose menu item "Run As->Ant Build"
+  -->
+
+<project name="privateBuildDeveloper" default="sleighCompile">
+
+	<property name="sleigh.compile.class" value="ghidra.pcodeCPort.slgh_compile.SleighCompile"/>
+
+	<!--Import optional ant properties.  GhidraDev Eclipse plugin produces this so this file can find the Ghidra installation-->
+	<import file="../.antProperties.xml" optional="false" />
+
+	<target name="sleighCompile">
+
+		<!-- If language module is detached from installation, get Ghidra installation directory path from imported properties -->
+		<property name="framework.path" value="${ghidra.install.dir}/Ghidra/Framework"/>
+
+		<path id="sleigh.class.path">
+			<fileset dir="${framework.path}/SoftwareModeling/lib">
+				<include name="*.jar"/>
+			</fileset>
+			<fileset dir="${framework.path}/Generic/lib">
+				<include name="*.jar"/>
+			</fileset>
+			<fileset dir="${framework.path}/Utility/lib">
+				<include name="*.jar"/>
+			</fileset>
+		</path>
+
+		<available classname="${sleigh.compile.class}" classpathref="sleigh.class.path" property="sleigh.compile.exists"/>
+
+		<fail unless="sleigh.compile.exists" />
+
+		<java classname="${sleigh.compile.class}"
+			classpathref="sleigh.class.path"
+			fork="true"
+			failonerror="true">
+			<jvmarg value="-Xmx2048M"/>
+			<arg value="-i"/>
+			<arg value="sleighArgs.txt"/>
+			<arg value="-a"/>
+			<arg value="./languages"/>
+		</java>
+
+	</target>
+
+</project>
diff --git a/Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.cspec b/Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.cspec
new file mode 100644
index 0000000000..248cad321e
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.cspec
@@ -0,0 +1,17 @@
+<?xml version="1.0" encoding="UTF-8"?>
+
+<compiler_spec>
+  <global>
+    <range space="rom"/>
+  </global>
+  <stackpointer register="SP" space="stack_buffer"/>
+
+  <default_proto>
+    <prototype name="__stdcall" extrapop="0" stackshift="0" strategy="register">
+      <input>
+      </input>
+      <output>
+      </output>
+    </prototype>
+  </default_proto>
+</compiler_spec>
diff --git a/Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.pspec b/Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.pspec
new file mode 100644
index 0000000000..4bcf8877de
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.pspec
@@ -0,0 +1,9 @@
+<?xml version="1.0" encoding="UTF-8"?>
+
+<processor_spec>
+  <default_symbols>
+    <symbol name="entry" address="rom:0000" entry="true"/>
+    <!-- 0x3FF * 3 -->
+    <symbol name="ISR" address="rom:0xBDF"/>
+  </default_symbols>
+</processor_spec>
diff --git a/Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.slaspec b/Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.slaspec
new file mode 100644
index 0000000000..76940ad4fd
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/data/languages/KCPSM3.slaspec
@@ -0,0 +1,374 @@
+# Specification for the PicoBlaze KCPSM3 processor
+
+# Known differences from actual KCPSM3:
+# - PC should be a 10-bit value with wraparound, Ghidra cannot handle non byte-aligned space sizes
+
+@include "picoblaze_common.sinc"
+
+
+define token opcode(24)
+	op0_17 = (0, 17)
+
+	op6_7 = (6,7)
+	op3_7 = (3,7)
+	op0_9 = (0,9)
+	op0_7 = (0,7)
+	op0_5 = (0,5)
+	op0_3 = (0,3)
+	op0_2 = (0,2)
+	op12_17 = (12, 17)
+	op10_11 = (10, 11)
+	op12 = (12, 12)
+	op13_17 = (13, 17)
+
+	reg8_11 = (8, 11)
+	reg4_7 = (4, 7)
+;
+
+attach variables [ reg8_11 reg4_7 ] [
+	s0 s1 s2 s3 s4 s5 s6 s7 s8 s9 sA sB sC sD sE sF
+];
+
+
+macro push(v) {
+    *[stack_buffer]:2 SP = v;
+    SP = (SP + 1) % 32;
+}
+
+
+# JUMP
+
+jtgt: tgt is op0_9 [ tgt = op0_9 * 3; ] {
+    local addr:3 = tgt;
+    export addr;
+}
+
+# Conditional
+with jcond: op12=1 {
+    :"Z"  is op10_11=0b00 {
+        local v:1 = Z != 0;
+        export v;
+    }
+    :"NZ"  is op10_11=0b01 {
+        local v:1 = Z == 0;
+        export v;
+    }
+    :"C"  is op10_11=0b10 {
+        local v:1 = C != 0;
+        export v;
+    }
+    :"NC" is op10_11=0b11 {
+        local v:1 = C == 0;
+        export v;
+    }
+}
+
+:"JUMP " jcond, jtgt is op13_17=0b11010 & jcond & jtgt {
+    if(jcond == 0) goto <end>;
+    goto [jtgt];
+    <end>
+}
+# Unconditional
+:"JUMP " jtgt is op13_17=0b11010 & op12=0 & jtgt {
+    goto [jtgt];
+}
+
+# CALL
+
+:"CALL " jcond, jtgt is op13_17=0b11000 & jcond & jtgt {
+    if(jcond == 0) goto <end>;
+    local tmp:2 = inst_start;
+    push(tmp);
+    call [jtgt];
+    <end>
+}
+# Unconditional
+:"CALL " jtgt is op13_17=0b11000 & op12=0 & jtgt {
+    local tmp:2 = inst_start;
+    push(tmp);
+    call [jtgt];
+}
+
+# RETURN
+
+:"RETURN " jcond  is op13_17=0b10101 & jcond & op0_9=0 {
+    if(jcond == 0) goto <end>;
+        SP = (SP - 1) % 32;
+        local tmp:2 = *[stack_buffer]:2 SP;
+        tmp = tmp + 3;
+        return [tmp];
+    <end>
+}
+# Unconditional
+:"RETURN" is op13_17=0b10101 & op12=0 & op0_9=0 {
+    SP = (SP - 1) % 32;
+    local tmp:2 = *[stack_buffer]:2 SP;
+    tmp = tmp + 3;
+    return [tmp];
+}
+
+# RETURNI
+
+:"RETURNI_ENABLE "  is op0_17=0b111000000000000001 {
+    SP = (SP - 1) % 32;
+    local tmp:2 = *[stack_buffer]:2 SP;
+    enable_interrupt();
+    return [tmp];
+}
+
+:"RETURNI_DISABLE "  is op0_17=0b111000000000000000 {
+    SP = (SP - 1) % 32;
+    local tmp:2 = *[stack_buffer]:2 SP;
+    disable_interrupt();
+    return [tmp];
+}
+
+# ENABLE/DISABLE INTERRUPT
+
+
+:ENABLE_INTERRUPT is op0_17=0b111100000000000001 {
+    enable_interrupt();
+}
+
+:DISABLE_INTERRUPT is op0_17=0b111100000000000000 {
+    disable_interrupt();
+}
+
+macro update_flags(v) {
+    C = 0;
+    Z = (v == 0);
+}
+
+# LOAD
+
+:LOAD reg8_11,op0_7 is op12_17=0b000000 & reg8_11 & op0_7 {
+	reg8_11 = op0_7;
+}
+
+:LOAD reg8_11,reg4_7 is op12_17=0b000001 & reg8_11 & reg4_7 & op0_3=0b0000 {
+	reg8_11 = reg4_7;
+}
+
+# AND
+
+:AND reg8_11,op0_7 is op12_17=0b001010 & reg8_11 & op0_7 {
+	reg8_11 = reg8_11 & op0_7;
+    update_flags(reg8_11);
+}
+
+:AND reg8_11,reg4_7 is op12_17=0b001011 & reg8_11 & reg4_7 & op0_3=0b0000 {
+	reg8_11 = reg8_11 & reg4_7;
+    update_flags(reg8_11);
+}
+
+# OR
+
+:OR reg8_11,op0_7 is op12_17=0b001100 & reg8_11 & op0_7 {
+	reg8_11 = reg8_11 | op0_7;
+    update_flags(reg8_11);
+}
+
+:OR reg8_11,reg4_7 is op12_17=0b001101 & reg8_11 & reg4_7 & op0_3=0b0000 {
+	reg8_11 = reg8_11 | reg4_7;
+    update_flags(reg8_11);
+}
+
+# XOR
+
+:XOR reg8_11,op0_7 is op12_17=0b001110 & reg8_11 & op0_7 {
+	reg8_11 = reg8_11 ^ op0_7;
+    update_flags(reg8_11);
+}
+
+:XOR reg8_11,reg4_7 is op12_17=0b001111 & reg8_11 & reg4_7 & op0_3=0b0000 {
+	reg8_11 = reg8_11 ^ reg4_7;
+    update_flags(reg8_11);
+}
+
+# TEST
+define pcodeop odd_parity;
+
+:TEST reg8_11,op0_7 is op12_17=0b010010 & reg8_11 & op0_7 {
+	local tmp:1 = reg8_11 & op0_7;
+    Z = tmp == 0;
+    C = odd_parity(tmp);
+}
+
+:TEST reg8_11,reg4_7 is op12_17=0b010010 & reg8_11 & reg4_7 & op0_3=0b0000 {
+	local tmp:1 = reg8_11 & reg4_7;
+    Z = tmp == 0;
+    C = odd_parity(tmp);
+}
+
+# ADD
+
+:ADD reg8_11,op0_7 is op12_17=0b011000 & reg8_11 & op0_7 {
+	reg8_11 = reg8_11 + op0_7;
+    C = carry(reg8_11, op0_7);
+    Z = (reg8_11 == 0);
+}
+
+:ADD reg8_11,reg4_7 is op12_17=0b011001 & reg8_11 & reg4_7 & op0_3=0b0000 {
+	reg8_11 = reg8_11 + reg4_7;
+    C = carry(reg8_11, reg4_7);
+    Z = (reg8_11 == 0);
+}
+
+# ADDCY
+
+:ADDCY reg8_11,op0_7 is op12_17=0b011010 & reg8_11 & op0_7 {
+    local tmp:1 = reg8_11 + op0_7;
+	reg8_11 = reg8_11 + op0_7 + C;
+    C = carry(reg8_11, op0_7) || carry(tmp, C);
+    Z = (reg8_11 == 0);
+}
+
+:ADDCY reg8_11,reg4_7 is op12_17=0b011010 & reg8_11 & reg4_7 & op0_3=0b0000 {
+    local tmp:1 = reg8_11 + reg4_7;
+	reg8_11 = reg8_11 + reg4_7 + C;
+    C = carry(reg8_11, reg4_7) || carry(tmp, C);
+    Z = (reg8_11 == 0);
+}
+
+# SUB
+
+:SUB reg8_11,op0_7 is op12_17=0b011100 & reg8_11 & op0_7 {
+	reg8_11 = reg8_11 - op0_7;
+    C = reg8_11 < op0_7;
+    Z = reg8_11 == 0;
+}
+
+:SUB reg8_11,reg4_7 is op12_17=0b011100 & reg8_11 & reg4_7 & op0_3=0b0000 {
+	reg8_11 = reg8_11 - reg4_7;
+    C = reg8_11 < reg4_7;
+    Z = reg8_11 == 0;
+}
+
+# SUBCY
+
+:SUBCY reg8_11,op0_7 is op12_17=0b011110 & reg8_11 & op0_7 {
+    local tmp:1 = reg8_11 - op0_7;
+	reg8_11 = reg8_11 - op0_7 - C;
+    C = reg8_11 < op0_7 || tmp < C;
+    Z = (reg8_11 == 0);
+}
+
+:SUBCY reg8_11,reg4_7 is op12_17=0b011111 & reg8_11 & reg4_7 & op0_3=0b0000 {
+    local tmp:1 = reg8_11 - reg4_7;
+	reg8_11 = reg8_11 - reg4_7 - C;
+    C = reg8_11 < reg4_7 || tmp < C;
+    Z = (reg8_11 == 0);
+}
+
+# COMPARE
+
+:COMPARE reg8_11,op0_7 is op12_17=0b010100 & reg8_11 & op0_7 {
+    Z = (reg8_11 == op0_7);
+    C = (reg8_11 < op0_7);
+}
+
+:COMPARE reg8_11,reg4_7 is op12_17=0b010101 & reg8_11 & reg4_7 & op0_3=0b0000 {
+    Z = (reg8_11 == reg4_7);
+    C = (reg8_11 < reg4_7);
+}
+
+# SR0 / SR1 / SRX / SRA / RR
+
+:SR0 reg8_11 is op12_17=0b100000 & reg8_11 & op3_7=0b00001 & op0_2=0b110 {
+    C = reg8_11 & 1;
+    reg8_11 = reg8_11 >> 1;
+    Z = reg8_11 == 0;
+}
+:SR1 reg8_11 is op12_17=0b100000 & reg8_11 & op3_7=0b00001 & op0_2=0b111 {
+    Z = 0;
+    C = reg8_11 & 1;
+    reg8_11 = (0b10000000 | ( reg8_11 >> 1));
+}
+:SRX reg8_11 is op12_17=0b100000 & reg8_11 & op3_7=0b00001 & op0_2=0b010 {
+    C = reg8_11 & 1;
+    reg8_11 = (( reg8_11 & 0b10000000) | ( reg8_11 >> 1));
+    Z = reg8_11 == 0;
+}
+:SRA reg8_11 is op12_17=0b100000 & reg8_11 & op3_7=0b00001 & op0_2=0b000 {
+    local tmp:1 = reg8_11 & 1;
+    reg8_11 = (( C << 7) | ( reg8_11 >> 1));
+    C = tmp;
+    Z = reg8_11 == 0;
+}
+:RR reg8_11 is op12_17=0b100000 & reg8_11 & op3_7=0b00001 & op0_2=0b100 {
+    C = reg8_11 & 1;
+    reg8_11 = (( C << 7) | ( reg8_11 >> 1));
+    Z = reg8_11 == 0;
+}
+
+# SL0 / SL1 / SLX / SLA / RL
+:SL0 reg8_11 is op12_17=0b100000 & reg8_11 & op3_7=0b00000 & op0_2=0b110 {
+    C = reg8_11 >> 7;
+    reg8_11 = reg8_11 << 1;
+    Z = reg8_11 == 0;
+}
+:SL1 reg8_11 is op12_17=0b100000 & reg8_11 & op3_7=0b00000 & op0_2=0b111 {
+    Z = 0;
+    C = reg8_11 >> 7;
+    reg8_11 = (1 | ( reg8_11 << 1));
+}
+:SLX reg8_11 is op12_17=0b100000 & reg8_11 & op3_7=0b00000 & op0_2=0b010 {
+    C = reg8_11 >> 7;
+    reg8_11 = (( reg8_11 & 1) | ( reg8_11 << 1));
+    Z = reg8_11 == 0;
+}
+:SLA reg8_11 is op12_17=0b100000 & reg8_11 & op3_7=0b00000 & op0_2=0b000 {
+    local tmp:1 = reg8_11 >> 7;
+    reg8_11 = ( C | ( reg8_11 << 1));
+    C = tmp;
+    Z = reg8_11 == 0;
+}
+:RL reg8_11 is op12_17=0b100000 & reg8_11 & op3_7=0b00000 & op0_2=0b100 {
+    C = reg8_11 >> 7;
+    reg8_11 = (( C ) | ( reg8_11 << 1));
+    Z = reg8_11 == 0;
+}
+
+# OUTPUT
+
+define pcodeop output;
+
+:OUTPUT reg8_11,op0_7 is op12_17=0b101100 & reg8_11 & op0_7 {
+    output(reg8_11, op0_7:1);
+}
+
+:OUTPUT reg8_11,reg4_7 is op12_17=0b101101 & reg8_11 & reg4_7 & op0_3=0b0000 {
+    output(reg8_11, reg4_7);
+}
+
+# INPUT
+
+define pcodeop input;
+
+:INPUT reg8_11,op0_7 is op12_17=0b000100 & reg8_11 & op0_7 {
+    reg8_11 = input(op0_7:1);
+}
+
+:INPUT reg8_11,reg4_7 is op12_17=0b000101 & reg8_11 & reg4_7 & op0_3=0b0000 {
+    reg8_11 = input(reg4_7);
+}
+
+# STORE
+
+:STORE reg8_11,op0_5 is op12_17=0b101110 & reg8_11 & op6_7=0 & op0_5 {
+    *[scratchpad]:1 op0_5:1 = reg8_11;
+}
+
+:STORE reg8_11,reg4_7 is op12_17=0b101111 & reg8_11 & reg4_7 & op0_3=0b0000 {
+    *[scratchpad]:1 reg4_7 = reg8_11;
+}
+
+# FETCH
+
+:FETCH reg8_11,op0_5 is op12_17=0b000110 & reg8_11 & op6_7=0 & op0_5 {
+    reg8_11 = *[scratchpad]:1 op0_5:1;
+}
+
+:FETCH reg8_11,reg4_7 is op12_17=0b000111 & reg8_11 & reg4_7 & op0_3=0b0000 {
+    reg8_11 = *[scratchpad]:1 reg4_7;
+}
diff --git a/Ghidra/Processors/PicoBlaze/data/languages/KCPSM6.pspec b/Ghidra/Processors/PicoBlaze/data/languages/KCPSM6.pspec
new file mode 100644
index 0000000000..4bcf8877de
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/data/languages/KCPSM6.pspec
@@ -0,0 +1,9 @@
+<?xml version="1.0" encoding="UTF-8"?>
+
+<processor_spec>
+  <default_symbols>
+    <symbol name="entry" address="rom:0000" entry="true"/>
+    <!-- 0x3FF * 3 -->
+    <symbol name="ISR" address="rom:0xBDF"/>
+  </default_symbols>
+</processor_spec>
diff --git a/Ghidra/Processors/PicoBlaze/data/languages/KCPSM6.slaspec b/Ghidra/Processors/PicoBlaze/data/languages/KCPSM6.slaspec
new file mode 100644
index 0000000000..ae768e408e
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/data/languages/KCPSM6.slaspec
@@ -0,0 +1,400 @@
+# Specification for the PicoBlaze KCPSM6 processor
+# Implementation based on this spec: https://web.archive.org/web/20240817205135/https://www.eng.auburn.edu/~nelsovp/courses/elec4200/PicoBlaze/kcpsm6.pdf
+
+# Known differences from actual KCPSM6:
+# - Stack overflows are not checked, on real implementations they cause a reset
+# - As in KCPSM3, PC wraparound is not supported
+
+@include "picoblaze_common.sinc"
+
+
+define token opcode(24)
+	opc=(12,17)
+	sX=(8,11)
+	sY=(4,7)
+	op0_3=(0,3)
+	kk=(0,7)
+	kk2=(4,11)
+	p=(0,3)
+	ins=(0,17)
+	aaa=(0,11)
+
+	op12_13=(12,13)
+	op14_16=(14,16)
+	op17=(17,17)
+;
+
+
+attach variables [ sX sY  ] [
+	s0 s1 s2 s3 s4 s5 s6 s7 s8 s9 sA sB sC sD sE sF
+];
+
+define bitrange regBank=flags[2,1];
+
+macro push(v) {
+	# In KCPSM6 stack overflow is a reset rather than a wraparound
+    *[stack_buffer]:2 SP = v;
+}
+
+# Loading
+
+:LOAD sX, sY is opc=0x00 & sX & sY & op0_3=0 {
+	sX = sY;
+}
+
+:LOAD sX, kk is opc=0x01 & sX & kk {
+	sX = kk;
+}
+
+define pcodeop move_banks;
+
+:STAR sX, sY is opc=0x16 & sX & sY & op0_3=0 {
+	move_banks(sX, sY);
+}
+
+# Logical
+
+:AND sX, sY is opc=0x02 & sX & sY & op0_3=0 {
+	sX = sX & sY;
+	C = 0;
+	Z = sX == 0;
+}
+:AND sX, kk is opc=0x03 & sX & kk {
+	sX = sX & kk;
+	C = 0;
+	Z = sX == 0;
+}
+
+:OR sX, sY is opc=0x04 & sX & sY & op0_3=0 {
+	sX = sX | sY;
+	C = 0;
+	Z = sX == 0;
+}
+:OR sX, kk is opc=0x05 & sX & kk {
+	sX = sX | kk;
+	C = 0;
+	Z = sX == 0;
+}
+
+:XOR sX, sY is opc=0x06 & sX & sY & op0_3=0 {
+	sX = sX ^ sY;
+	C = 0;
+	Z = sX == 0;
+}
+:XOR sX, kk is opc=0x07 & sX & kk {
+	sX = sX ^ kk;
+	C = 0;
+	Z = sX == 0;
+}
+
+# Arithmetic
+
+:ADD sX, sY is opc=0x10 & sX & sY & op0_3=0 {
+	C = carry(sX, sY);
+	sX = sX + sY;
+	Z = sX == 0;
+}
+:ADD sX, kk is opc=0x11 & sX & kk {
+	C = carry(sX, kk);
+	sX = sX + kk;
+	Z = sX == 0;
+}
+
+:ADDCY sX, sY is opc=0x12 & sX & sY & op0_3=0 {
+	local tmp:2 = zext(sX) + zext(sY) + zext(C);
+	C = tmp > 0xFF;
+	sX = tmp:1;
+	Z = (sX == 0) & Z;
+}
+:ADDCY sX, kk is opc=0x13 & sX & kk {
+	local tmp:2 = zext(sX) + zext(kk:1) + zext(C);
+	C = tmp > 0xFF;
+	sX = tmp:1;
+	Z = (sX == 0) & Z;
+}
+
+:SUB sX, sY is opc=0x18 & sX & sY & op0_3=0 {
+	C = scarry(sX, sY);
+	sX = sX - sY;
+	Z = sX == 0;
+}
+:SUB sX, kk is opc=0x19 & sX & kk {
+	C = scarry(sX, kk);
+	sX = sX - kk;
+	Z = sX == 0;
+}
+
+:SUBCY sX, sY is opc=0x1A & sX & sY & op0_3=0 {
+	local tmp:2 = zext(sX) - zext(sY) - zext(C);
+	C = tmp s< 0;
+	sX = tmp:1;
+	Z = (sX == 0) & Z;
+}
+:SUBCY sX, kk is opc=0x1B & sX & kk {
+	local tmp:2 = zext(sX) - zext(kk:1) - zext(C);
+	C = tmp s< 0;
+	sX = tmp:1;
+	Z = (sX == 0) & Z;
+}
+
+# Test and compare
+
+define pcodeop odd_parity;
+
+:TEST sX, sY is opc=0x0C & sX & sY & op0_3=0 {
+	local tmp:1 = sX & sY;
+    Z = tmp == 0;
+    C = odd_parity(tmp);
+}
+:TEST sX, kk is opc=0x0D & sX & kk {
+	local tmp:1 = sX & kk;
+    Z = tmp == 0;
+    C = odd_parity(tmp);
+}
+
+:TESTCY sX, sY is opc=0x0E & sX & sY & op0_3=0 {
+	local tmp:1 = sX & sY;
+    Z = (tmp == 0) & Z;
+    C = odd_parity(tmp, C);
+}
+:TESTCY sX, kk is opc=0x0F & sX & kk {
+	local tmp:1 = sX & kk;
+    Z = (tmp == 0) & Z;
+    C = odd_parity(tmp, C);
+}
+
+:COMPARE sX, sY is opc=0x1C & sX & sY & op0_3=0 {
+	local tmp:1 = sX - sY;
+    Z = (tmp == 0);
+    C = scarry(sX, sY);
+}
+:COMPARE sX, kk is opc=0x1D & sX & kk {
+	local tmp:1 = sX - kk;
+    Z = (tmp == 0);
+    C = scarry(sX, kk);
+}
+
+:COMPARECY sX, sY is opc=0x1E & sX & sY & op0_3=0 {
+	local tmp:2 = zext(sX) - zext(sY) - zext(C);
+    Z = (tmp:1 == 0) & Z;
+    C = tmp s< 0;
+}
+:COMPARECY sX, kk is opc=0x1F & sX & kk {
+	local tmp:2 = zext(sX) - zext(kk:1) - zext(C);
+    Z = (tmp:1 == 0) & Z;
+    C = tmp s< 0;
+}
+
+# Shift and Rotate
+
+:SL0 sX is opc=0x14 & sX & kk=0x06 {
+	C = sX >> 7;
+    sX = sX << 1;
+    Z = sX == 0;
+}
+:SL1 sX is opc=0x14 & sX & kk=0x07 {
+	Z = 0;
+    C = sX >> 7;
+    sX = (1 | ( sX << 1));
+}
+:SLX sX is opc=0x14 & sX & kk=0x04 {
+	C = sX >> 7;
+    sX = (( sX & 1) | ( sX << 1));
+    Z = sX == 0;
+}
+:SLA sX is opc=0x14 & sX & kk=0x00 {
+	local tmp:1 = sX >> 7;
+    sX = ( C | ( sX << 1));
+    C = tmp;
+    Z = sX == 0;
+}
+:RL sX is opc=0x14 & sX & kk=0x02 {
+	C = sX >> 7;
+    sX = (( C ) | ( sX << 1));
+    Z = sX == 0;
+}
+
+:SR0 sX is opc=0x14 & sX & kk=0x0E {
+	C = sX & 1;
+    sX = sX >> 1;
+    Z = sX == 0;
+}
+:SR1 sX is opc=0x14 & sX & kk=0x0F {
+	Z = 0;
+    C = sX & 1;
+    sX = (0b10000000 | ( sX >> 1));
+}
+:SRX sX is opc=0x14 & sX & kk=0x0A {
+	C = sX & 1;
+    sX = (( sX & 0b10000000) | ( sX >> 1));
+    Z = sX == 0;
+}
+:SRA sX is opc=0x14 & sX & kk=0x08 {
+	local tmp:1 = sX & 1;
+    sX = (( C << 7) | ( sX >> 1));
+    C = tmp;
+    Z = sX == 0;
+}
+:RR sX is opc=0x14 & sX & kk=0x0C {
+	C = sX & 1;
+    sX = (( C << 7) | ( sX >> 1));
+    Z = sX == 0;
+}
+
+# Register Bank
+
+define pcodeop regbank_a;
+define pcodeop regbank_b;
+
+:"REGBANK A" is ins=0x37000 {
+    regbank_a();
+}
+
+:"REGBANK B" is ins=0x37001 {
+    regbank_b();
+}
+
+# Input and Output
+
+define pcodeop input;
+define pcodeop output;
+
+:INPUT sX, sY is opc=0x08 & sX & sY & op0_3=0 {
+	sX = input(sY);
+}
+:INPUT sX, kk is opc=0x09 & sX & kk {
+	sX = input(kk:1);
+}
+
+:OUTPUT sX, sY is opc=0x2C & sX & sY & op0_3=0 {
+	output(sX, sY);
+}
+:OUTPUT sX, kk is opc=0x2D & sX & kk {
+	output(sX, kk:1);
+}
+
+:OUTPUTK kk2, p is opc=0x2B & kk2 & p {
+	output(kk2:1, p:1);
+}
+
+# Scratch pad memory
+
+:STORE sX, sY is opc=0x2E & sX & sY & op0_3=0 {
+	*[scratchpad]:1 sX = sY;
+}
+
+:STORE sX, kk is opc=0x2F & sX & kk {
+	*[scratchpad]:1 kk:1 = sX;
+}
+
+:FETCH sX, sY is opc=0x0A & sX & sY & op0_3=0 {
+	sX = *[scratchpad]:1 sY;
+}
+
+:FETCH sX, kk is opc=0x0B & sX & kk {
+	sX = *[scratchpad]:1 kk:1;
+}
+
+# Interrupt handling
+
+:DISABLE_INTERRUPT is ins=0x28000 {
+    disable_interrupt();
+}
+:ENABLE_INTERRUPT is ins=0x28001 {
+    enable_interrupt();
+}
+
+# On interrupt Z, C and REGBANK are pushed with PC, on RETURNI they are restored
+# This simulates that, as we don't model the interrupt "push" side of this
+define pcodeop restore_interrupt_flags;
+:RETURNI_DISABLE is ins=0x29000 {
+    SP = SP - 1;
+    local tmp:2 = *[stack_buffer]:2 SP;
+    disable_interrupt();
+    flags = restore_interrupt_flags();
+    return [tmp];
+}
+:RETURNI_ENABLE is ins=0x29001 {
+    SP = SP - 1;
+    local tmp:2 = *[stack_buffer]:2 SP;
+    enable_interrupt();
+    flags = restore_interrupt_flags();
+    return [tmp];
+}
+
+# Jump
+
+jcond:"Z" is op14_16=0b100 { local a:1 = Z != 0; export a; }
+jcond:"NZ" is op14_16=0b101 { local a:1 = Z == 0; export a; }
+jcond:"C" is op14_16=0b110 { local a:1 = C != 0; export a; }
+jcond:"NC" is op14_16=0b111 { local a:1 = C == 0; export a; }
+
+:JUMP tgt is op12_13=0b10 & op14_16=0b000 & op17=1 & aaa [tgt = aaa * 3;] {
+	local a:2 = tgt;
+	goto [a];
+}
+:JUMP jcond, tgt is op12_13=0b10 & jcond & op17=1 & aaa [tgt = aaa * 3;]  {
+	if(jcond == 0) goto <end>;
+		local a:2 = tgt;
+		goto [a];
+	<end>
+}
+:JUMP@ (sX, sY) is opc=0x26 & sX & sY & op0_3=0 {
+	local tmp:2 = zext(sY) | ((zext(sX) & 0xF) << 8);
+	tmp = tmp * 3;
+	goto [tmp];
+}
+
+:CALL tgt is op12_13=0b00 & op14_16=0b000 & op17=1 & aaa [tgt = aaa * 3;] {
+	local tmp:2 = inst_start;
+    push(tmp);
+    local a:2 = tgt;
+    call [a];
+}
+:CALL jcond, tgt is op12_13=0b00 & jcond & op17=1 & aaa [tgt = aaa * 3;] {
+	if(jcond == 0) goto <end>;
+	    local tmp:2 = inst_start;
+	    push(tmp);
+	    local a:2 = tgt;
+	    call [a];
+    <end>
+}
+:CALL@ (sX, sY) is opc=0x24 & sX & sY & op0_3=1 {
+	local tmp:2 = inst_start;
+    push(tmp);
+	local tmp2:2 = zext(sY) | ((zext(sX) & 0xF) << 8);
+	call [tmp2];
+}
+
+:RETURN is ins=0x25000 {
+    SP = SP - 1;
+    local tmp:2 = *[stack_buffer]:2 SP;
+    tmp = tmp + 3;
+    return [tmp];
+}
+:RETURN jcond is op12_13=0b01 & jcond & op17=1 & aaa=0 {
+	if(jcond == 0) goto <end>;
+        SP = SP - 1;
+        local tmp:2 = *[stack_buffer]:2 SP;
+        tmp = tmp + 3;
+        return [tmp];
+    <end>
+}
+:LOAD_RETURN sX, kk is opc=0x21 & sX & kk {
+	sX = kk;
+
+	SP = SP - 1;
+    local tmp:2 = *[stack_buffer]:2 SP;
+    tmp = tmp + 3;
+    return [tmp];
+}
+
+# Version control
+
+define pcodeop hwbuild;
+
+:HWBUILD sX is opc=0x14 & sX & kk=0x80 {
+	C = 1;
+	sX = hwbuild();
+	Z = sX == 0;
+}
\ No newline at end of file
diff --git a/Ghidra/Processors/PicoBlaze/data/languages/picoblaze.ldefs b/Ghidra/Processors/PicoBlaze/data/languages/picoblaze.ldefs
new file mode 100644
index 0000000000..e21e2c2f76
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/data/languages/picoblaze.ldefs
@@ -0,0 +1,30 @@
+<?xml version="1.0" encoding="UTF-8"?>
+
+
+<language_definitions>
+
+   <language processor="PicoBlaze"
+            endian="big"
+            size="24"
+            variant="KCPSM3"
+            version="1.0"
+            slafile="KCPSM3.sla"
+            processorspec="KCPSM3.pspec"
+            id="KCPSM3:BE:24:default">
+    <description>PicoBlaze KCPSM3</description>
+    <compiler name="default" spec="KCPSM3.cspec" id="default"/>
+  </language>
+
+      <language processor="PicoBlaze"
+            endian="big"
+            size="24"
+            variant="KCPSM6"
+            version="1.0"
+            slafile="KCPSM6.sla"
+            processorspec="KCPSM6.pspec"
+            id="KCPSM6:BE:24:default">
+                <description>PicoBlaze KCPSM6</description>
+    <compiler name="default" spec="KCPSM3.cspec" id="default"/>
+  </language>
+
+</language_definitions>
diff --git a/Ghidra/Processors/PicoBlaze/data/languages/picoblaze_common.sinc b/Ghidra/Processors/PicoBlaze/data/languages/picoblaze_common.sinc
new file mode 100644
index 0000000000..c0d49c1be9
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/data/languages/picoblaze_common.sinc
@@ -0,0 +1,21 @@
+define endian=big;
+define alignment=3;
+
+# This should be rom_space but thats not actually supported in the sleigh compiler
+define space rom            type=ram_space      size=3   wordsize=1 default;
+define space scratchpad     type=ram_space      size=1   wordsize=1;
+define space stack_buffer   type=ram_space      size=1   wordsize=1;
+define space register       type=register_space size=1;
+
+define register offset=0x00 size=1 [
+	s0 s1 s2 s3 s4 s5 s6 s7 s8 s9 sA sB sC sD sE sF
+    SP
+    flags
+];
+
+define bitrange Z=flags[0,1]
+                C=flags[1,1]
+;
+
+define pcodeop enable_interrupt;
+define pcodeop disable_interrupt;
\ No newline at end of file
diff --git a/Ghidra/Processors/PicoBlaze/data/sleighArgs.txt b/Ghidra/Processors/PicoBlaze/data/sleighArgs.txt
new file mode 100644
index 0000000000..ce06926358
--- /dev/null
+++ b/Ghidra/Processors/PicoBlaze/data/sleighArgs.txt
@@ -0,0 +1,6 @@
+# Add sleigh compiler options to this file (one per line) which will
+# be used when compiling each language within this module.
+# All options should start with a '-' character.
+#
+# IMPORTANT: The -a option should NOT be specified
+#
\ No newline at end of file
-- 
2.45.1

