From 0000000000000000000000000000000000000000 Mon Sep 17 00:00:00 2001
From: b1n4ri0 <76442881+b1n4ri0@users.noreply.github.com>
Date: Thu, 1 Jan 2026 22:57:10 +0100
Subject: [PATCH] 8848: RISC-V: Update Bit-Manipulation (B) extension to v1.0.0

Implement RV32 zba instructions.

Implement RV32 zbb instructions.

Implement RV32 zbs instructions.

Implement RV32 zbkb instructions.

Remove obsolete Bitmanip v0.92 draft instructions

Implement RV64 zba instructions.

Implement RV64 zbb instructions.

Implement RV64 zbs instructions.

Implement RV64 zbkb instructions.

Remove RV64 obsolete Bitmanip v0.92 draft instructions

Fix RV64 packw and cpopw instructions, add HXLEN constant to lp64d and ilp32d.

Add Half-XLEN comment to HXLEN.
---
 .../RISCV/data/languages/riscv.ilp32d.slaspec |   1 +
 .../RISCV/data/languages/riscv.lp64d.slaspec  |   1 +
 .../RISCV/data/languages/riscv.rv32b.sinc     | 409 +++++++++++-------
 .../RISCV/data/languages/riscv.rv64b.sinc     | 245 ++++++-----
 4 files changed, 402 insertions(+), 254 deletions(-)

diff --git a/Ghidra/Processors/RISCV/data/languages/riscv.ilp32d.slaspec b/Ghidra/Processors/RISCV/data/languages/riscv.ilp32d.slaspec
index ee9976d0fb..24c80ffac0 100644
--- a/Ghidra/Processors/RISCV/data/languages/riscv.ilp32d.slaspec
+++ b/Ghidra/Processors/RISCV/data/languages/riscv.ilp32d.slaspec
@@ -1,5 +1,6 @@
 define endian=little;
 
+@define HXLEN 2 # Half-XLEN
 @define XLEN 4
 @define XLEN2 8
 @define FLEN 8
diff --git a/Ghidra/Processors/RISCV/data/languages/riscv.lp64d.slaspec b/Ghidra/Processors/RISCV/data/languages/riscv.lp64d.slaspec
index f1d11a233a..6e5faaf595 100644
--- a/Ghidra/Processors/RISCV/data/languages/riscv.lp64d.slaspec
+++ b/Ghidra/Processors/RISCV/data/languages/riscv.lp64d.slaspec
@@ -1,5 +1,6 @@
 define endian=little;
 
+@define HXLEN 4 # Half-XLEN
 @define XLEN 8
 @define XLEN2 16
 @define FLEN 8
diff --git a/Ghidra/Processors/RISCV/data/languages/riscv.rv32b.sinc b/Ghidra/Processors/RISCV/data/languages/riscv.rv32b.sinc
index 9deafd0f46..5d907c4906 100644
--- a/Ghidra/Processors/RISCV/data/languages/riscv.rv32b.sinc
+++ b/Ghidra/Processors/RISCV/data/languages/riscv.rv32b.sinc
@@ -1,149 +1,268 @@
-# RV32 Bitmanip Extension
+# RV32 Zb Extension (Bit Manipulation)
+# NOTE: v1.0.0
+# Reference: https://docs.riscv.org/reference/isa/unpriv/b-st-ext.html
+
 imm2024: op2024 is op2024 {local tmp:$(XLEN) = op2024; export tmp;}
 imm2025: op2025 is op2025 {local tmp:$(XLEN) = op2025; export tmp;}
 imm2026: op2026 is op2026 {local tmp:$(XLEN) = op2026; export tmp;}
 
-define pcodeop add_uw;
-:zext.w rd, rs1      is op0006=0x3b & op1214=0x0 & op1519=0x0 & op2531=0x04 & rd & rs1       {rd=zext(rs1:4);}
-:add.uw rd, rs1, rs2 is op0006=0x3b & op1214=0x0              & op2531=0x04 & rd & rs1 & rs2 {rd=zext(rs1:4)+rs2;}
-
-:andn rd, rs1, rs2 is op0006=0x33 & op1214=0x7 & op2531=0x20 & rd & rs1 & rs2 {rd=rs1 & ~rs2;}
-
-:bext rd, rs1, rs2 is op0006=0x33 & op1214=0x5 & op2531=0x24 & rd & rs1 & rs2 {rd=(rs1 >> rs2) & 1;}
-
-define pcodeop clmul;
-:clmul  rd, rs1, rs2 is op0006=0x33 & op1214=0x1 & op2531=0x5 & rd & rs1 & rs2 {rd=clmul(rs1,rs2);}
-
-define pcodeop clmulh;
-:clmulh rd, rs1, rs2 is op0006=0x33 & op1214=0x3 & op2531=0x5 & rd & rs1 & rs2 {rd=clmulh(rs1,rs2);}
-
-define pcodeop clmulr;
-:clmulr rd, rs1, rs2 is op0006=0x33 & op1214=0x2 & op2531=0x5 & rd & rs1 & rs2 {rd=clmulr(rs1,rs2);}
-
-define pcodeop clz;
-:clz rd, rs1 is op0006=0x13 & op1214=0x1 & op2024=0x0 & op2531=0x30 & rd & rs1 {rd=clz(rs1);}
-
-define pcodeop clzw;
-:clzw rd, rs1 is op0006=0x1b & op1214=0x1 & op2024=0x0 & op2531=0x30 & rd & rs1 {rd=clzw(rs1);}
-
-define pcodeop ctz;
-:ctz rd, rs1 is op0006=0x13 & op1214=0x1 & op2024=0x1 & op2531=0x30 & rd & rs1 {rd=ctz(rs1);}
-
-define pcodeop ctzw;
-:ctzw rd, rs1 is op0006=0x1b & op1214=0x1 & op2024=0x1 & op2531=0x30 & rd & rs1 {rd=ctzw(rs1);}
-
-define pcodeop orc_b;
-:orc.b rd, rs1 is op0006=0x13 & rd & op1214=0x5 & rs1 & op2025=0x07 & op2631=0x0a {rd=orc_b(rs1);}
-
-define pcodeop rev8;
-define pcodeop rev_b;
-#define pcodeop grevi;
-#                   rev8 has different encodings for 32 and 64 bit CPUs
-:rev.b rd, rs1         is op0006=0x13 & op1214=0x5 & op2731=0xd & op2026=0x7  & rd & rs1 {rd=rev_b(rs1);}
-:rev8  rd, rs1         is op0006=0x13 & op1214=0x5 & op2731=0xd & op2026=0x18 & rd & rs1 {rd=rev8(rs1);}
-:rev8  rd, rs1         is op0006=0x13 & op1214=0x5 & op2731=0xd & op2026=0x38 & rd & rs1 {rd=rev8(rs1);}
-
-define pcodeop minu;
-:minu  rd, rs1, rs2 is op0006=0x33 & op1214=0x5 & op2531=0x5 & rd & rs1 & rs2 {rd=minu(rs1,rs2);}
-
-define pcodeop maxu;
-:maxu rd, rs1, rs2 is op0006=0x33 & op1214=0x7 & op2531=0x5 & rd & rs1 & rs2 {rd=maxu(rs1,rs2);}
-
-define pcodeop min;
-:min  rd, rs1, rs2 is op0006=0x33 & op1214=0x4 & op2531=0x5 & rd & rs1 & rs2 {rd=min(rs1,rs2);}
-
-define pcodeop max;
-:max rd, rs1, rs2 is op0006=0x33 & op1214=0x6 & op2531=0x5 & rd & rs1 & rs2 {rd=max(rs1,rs2);}
-
-:orn  rd, rs1, rs2 is op0006=0x33 & op1214=0x6 & op2531=0x20 & rd & rs1 & rs2 {rd=rs1 | ~rs2;}
-
-define pcodeop pack;
-:pack  rd, rs1, rs2 is op0006=0x33 & op1214=0x4 & op2531=0x4 & rd & rs1 & rs2 {rd=pack(rs1,rs2);}
-
-define pcodeop packh;
-:packh rd, rs1, rs2 is op0006=0x33 & op1214=0x7 & op2531=0x4 & rd & rs1 & rs2 {rd=packh(rs1,rs2);}
-
-define pcodeop packw;
-:packw rd, rs1, rs2 is op0006=0x3b & op1214=0x4 & op2531=0x04 & rd & rs1 & rs2 {rd=packw(rs1,rs2);}
-
-define pcodeop zext_h;
-:zext.h rd, rs1 is op0006=0x3b & op1214=0x4 & op2531=0x04 & rd & op2024=0 & rs1 {rd=zext_h(rs1);}
-
-define pcodeop cpop;
-:cpop rd, rs1 is op0006=0x13 & op1214=0x1 & op2024=0x2 & op2531=0x30 & rd & rs1 {rd=cpop(rs1);}
-
-define pcodeop cpopw;
-:cpopw rd, rs1 is op0006=0x1b & op1214=0x1 & op2024=0x2 & op2531=0x30 & rd & rs1 {rd=cpopw(rs1);}
-
-define pcodeop rol;
-:rol  rd, rs1, rs2 is op0006=0x33 & op1214=0x1 & op2531=0x30 & rd & rs1 & rs2 {rd=rol(rs1,rs2);}
-
-define pcodeop rolw;
-:rolw  rd, rs1, rs2 is op0006=0x3b & op1214=0x1 & op2531=0x30 & rd & rs1 & rs2 {rd=rolw(rs1,rs2);}
-
-define pcodeop ror;
-:ror  rd, rs1, rs2 is op0006=0x33 & op1214=0x5 & op2531=0x30 & rd & rs1 & rs2 {rd=ror(rs1,rs2);}
-
-define pcodeop rorw;
-:rorw  rd, rs1, rs2 is op0006=0x3b & op1214=0x5 & op2531=0x30 & rd & rs1 & rs2 {rd=rorw(rs1,rs2);}
-
-define pcodeop rori;
-:rori rd, rs1, imm2026 is op0006=0x13 & op1214=0x5 & op2731=0x0c & imm2026 & rd & rs1 {rd=rori(rs1,imm2026);}
-
-define pcodeop roriw;
-:roriw rd, rs1, imm2024 is op0006=0x1b & op1214=0x5 & op2531=0x30 & imm2024 & rd & rs1 {rd=roriw(rs1,imm2024);}
-
-define pcodeop bclr;
-:bclr  rd, rs1, rs2 is op0006=0x33 & op1214=0x1 & op2531=0x24 & rd & rs1 & rs2 {rd=bclr(rs1,rs2);}
-
-define pcodeop bclri;
-:bclri rd, rs1, imm2026 is op0006=0x13 & op1214=0x1 & op2731=0x9 & imm2026 & rd & rs1 {rd=bclri(rs1,imm2026);}
-
-define pcodeop bexti;
-:bexti rd, rs1, imm2026 is op0006=0x13 & op1214=0x5 & op2731=0x9 & imm2026 & rd & rs1 {rd=bexti(rs1,imm2026);}
-
-define pcodeop binv;
-:binv  rd, rs1, rs2 is op0006=0x33 & op1214=0x1 & op2531=0x34 & rd & rs1 & rs2 {rd=binv(rs1,rs2);}
-
-define pcodeop binvi;
-:binvi rd, rs1, imm2026 is op0006=0x13 & op1214=0x1 & op2731=0xd & imm2026 & rd & rs1 {rd=binvi(rs1,imm2026);}
-
-define pcodeop bset;
-:bset  rd, rs1, rs2 is op0006=0x33 & op1214=0x1 & op2531=0x14 & rd & rs1 & rs2 {rd=bset(rs1,rs2);}
-
-define pcodeop bseti;
-:bseti rd, rs1, imm2026 is op0006=0x13 & op1214=0x1 & op2731=0x5 & imm2026 & rd & rs1 {rd=bseti(rs1,imm2026);}
-
-define pcodeop sext_b;
-:sext.b rd, rs1 is op0006=0x13 & op1214=0x1 & op2024=0x4 & op2531=0x30 & rd & rs1 {rd=sext_b(rs1);}
-
-define pcodeop sext_h;
-:sext.h rd, rs1 is op0006=0x13 & op1214=0x1 & op2024=0x5 & op2531=0x30 & rd & rs1 {rd=sext_h(rs1);}
-
-:sh1add rd, rs1, rs2 is op0006=0x33 & op1214=0x2 & op2531=0x10 & rd & rs1 & rs2 {rd=rs2+rs1<<1;}
-
-:sh1add.uw rd, rs1, rs2 is op0006=0x3b & op1214=0x2 & op2531=0x10 & rd & rs1 & rs2 {rd=rs2+zext(rs1<<1);}
-
-:sh2add rd, rs1, rs2 is op0006=0x33 & op1214=0x4 & op2531=0x10 & rd & rs1 & rs2 {rd=rs2+(rs1<<2);}
-
-:sh2add.uw rd, rs1, rs2 is op0006=0x3b & op1214=0x4 & op2531=0x10 & rd & rs1 & rs2 {rd=rs2+zext(rs1<<2);}
-
-:sh3add rd, rs1, rs2 is op0006=0x33 & op1214=0x6 & op2531=0x10 & rd & rs1 & rs2 {rd=rs2+(rs1<<3);}
-
-:sh3add.uw rd, rs1, rs2 is op0006=0x3b & op1214=0x6 & op2531=0x10 & rd & rs1 & rs2 {rd=rs2+zext(rs1<<3);}
-
-:slli.uw rd, rs1, imm2025 is op0006=0x1b & op1214=0x1 & op2631=0x2 & imm2025 & rd & rs1 {rd=zext(rs1:4)<<imm2025;}
-
-:xnor rd, rs1, rs2 is op0006=0x33 & op1214=0x4 & op2531=0x20 & rd & rs1 & rs2 {rd=~(rs1^rs2);}
-
-define pcodeop unzip;
-:unzip rd, rs1 is op0006=0x13 & op1214=0x5 & op2024=0x1f & rd & rs1 & op2531=0x04 {rd=unzip(rs1);}
-
-define pcodeop zip;
-:zip rd, rs1 is op0006=0x13 & op1214=0x1 & op2024=0x1e & rd & rs1 & op2531=0x04 {rd=zip(rs1);}
-
-define pcodeop xperm_b;
-:xperm.b rd, rs1, rs2 is op0006=0x33 & rd & op1214=0x4 & rs1 & rs2 & op2531=0x14 {rd=xperm_b(rs1,rs2);}
-
-define pcodeop xperm_n;
-:xperm.nf rd, rs1, rs2 is op0006=0x33 & rd & op1214=0x2 & rs1 & rs2 & op2531=0x14 {rd=xperm_n(rs1,rs2);}
+# ==============================================================================
+# Zba Instructions
+# ==============================================================================
+
+# 0010000 rs2 rs1 010 rd 0110011
+:sh1add rd, rs1, rs2    is op2531=0x10 & rs2 & rs1 & op1214=0x2 & rd & op0006=0x33
+{
+    rd = rs2 + (rs1 << 1);
+}
+
+# 0010000 rs2 rs1 100 rd 0110011
+:sh2add rd, rs1, rs2    is op2531=0x10 & rs2 & rs1 & op1214=0x4 & rd & op0006=0x33
+{
+    rd = rs2 + (rs1 << 2);
+}
+
+# 0010000 rs2 rs1 110 rd 0110011
+:sh3add rd, rs1, rs2    is op2531=0x10 & rs2 & rs1 & op1214=0x6 & rd & op0006=0x33
+{
+    rd = rs2 + (rs1 << 3);
+}
+
+# ==============================================================================
+# Zbb Instructions
+# ==============================================================================
+
+# 0100000 rs2 rs1 111 rd 0110011
+:andn rd, rs1, rs2      is op2531=0x20 & rs2 & rs1 & op1214=0x7 & rd & op0006=0x33
+{
+    rd = rs1 & ~rs2;
+}
+
+# 0100000 rs2 rs1 110 rd 0110011
+:orn rd, rs1, rs2       is op2531=0x20 & rs2 & rs1 & op1214=0x6 & rd & op0006=0x33
+{
+    rd = rs1 | ~rs2;
+}
+
+# 0100000 rs2 rs1 100 rd 0110011
+:xnor rd, rs1, rs2      is op2531=0x20 & rs2 & rs1 & op1214=0x4 & rd & op0006=0x33
+{
+    rd = ~(rs1 ^ rs2);
+}
+
+# 0110000 00010 rs1 001 rd 0010011
+:cpop rd, rs1           is op2531=0x30 & op2024=0x2 & rs1 & op1214=0x1 & rd & op0006=0x13
+{
+    rd = popcount(rs1);
+}
+
+# 0000101 rs2 rs1 110 rd 0110011
+:max rd, rs1, rs2       is op2531=0x5 & rs2 & rs1 & op1214=0x6 & rd & op0006=0x33
+{
+    if (rs1 s< rs2) goto <src2>;
+    rd = rs1;
+    goto <done>;
+    <src2>
+    rd = rs2;
+    <done>
+}
+
+# 0000101 rs2 rs1 111 rd 0110011
+:maxu rd, rs1, rs2      is op2531=0x5 & rs2 & rs1 & op1214=0x7 & rd & op0006=0x33
+{
+    if (rs1 < rs2) goto <src2>;
+    rd = rs1;
+    goto <done>;
+    <src2>
+    rd = rs2;
+    <done>
+}
+
+# 0000101 rs2 rs1 100 rd 0110011
+:min rd, rs1, rs2       is op2531=0x5 & rs2 & rs1 & op1214=0x4 & rd & op0006=0x33
+{
+    if (rs1 s< rs2) goto <src1>;
+    rd = rs2;
+    goto <done>;
+    <src1>
+    rd = rs1;
+    <done>
+}
+
+# 0000101 rs2 rs1 101 rd 0110011
+:minu rd, rs1, rs2      is op2531=0x5 & rs2 & rs1 & op1214=0x5 & rd & op0006=0x33
+{
+    if (rs1 < rs2) goto <src1>;
+    rd = rs2;
+    goto <done>;
+    <src1>
+    rd = rs1;
+    <done>
+}
+
+# 0110000 00100 rs1 001 rd 0010011
+:sext.b rd, rs1         is op2531=0x30 & op2024=0x4 & rs1 & op1214=0x1 & rd & op0006=0x13
+{
+    rd = sext(rs1:1);
+}
+
+# 0110000 00101 rs1 001 rd 0010011
+:sext.h rd, rs1         is op2531=0x30 & op2024=0x5 & rs1 & op1214=0x1 & rd & op0006=0x13
+{
+    rd = sext(rs1:2);
+}
+
+# 0000100 00000 rs1 100 rd 0110011
+:zext.h rd, rs1         is op2531=0x4 & op2024=0x0 & rs1 & op1214=0x4 & rd & op0006=0x33
+{
+    rd = zext(rs1:2);
+}
+
+# 0010100 00111 rs1 101 rd 0010011
+:orc.b rd, rs1          is op2031=0x287 & rs1 & op1214=0x5 & rd & op0006=0x13
+{
+    rd = (zext((rs1 & 0xFF) != 0) * 0xFF) |
+         (zext((rs1 & 0xFF00) != 0) * 0xFF00) |
+         (zext((rs1 & 0xFF0000) != 0) * 0xFF0000) |
+         (zext((rs1 & 0xFF000000) != 0) * 0xFF000000) |
+         (zext((rs1 & 0xFF00000000) != 0) * 0xFF00000000) |
+         (zext((rs1 & 0xFF0000000000) != 0) * 0xFF0000000000) |
+         (zext((rs1 & 0xFF000000000000) != 0) * 0xFF000000000000) |
+         (zext((rs1 & 0xFF00000000000000) != 0) * 0xFF00000000000000);
+}
+
+# ==============================================================================
+# Zbs Instructions
+# ==============================================================================
+
+# 0100100 rs2 rs1 001 rd 0110011
+:bclr rd, rs1, rs2      is op2531=0x24 & rs2 & rs1 & op1214=0x1 & rd & op0006=0x33
+{
+    rd = rs1 & ~(1:$(XLEN) << (rs2 & (($(XLEN) << 3) - 1)));
+}
+
+# 0100100 shamt rs1 001 rd 0010011
+:bclri rd, rs1, op2024  is op2531=0x24 & op2024 & rs1 & op1214=0x1 & rd & op0006=0x13
+{
+    rd = rs1 & ~(1:$(XLEN) << op2024);
+}
+
+# 0100100 rs2 rs1 101 rd 0110011
+:bext rd, rs1, rs2      is op2531=0x24 & rs2 & rs1 & op1214=0x5 & rd & op0006=0x33
+{
+    rd = (rs1 >> (rs2 & (($(XLEN) << 3) - 1))) & 1;
+}
+
+# 0100100 shamt rs1 101 rd 0010011
+:bexti rd, rs1, op2024  is op2531=0x24 & op2024 & rs1 & op1214=0x5 & rd & op0006=0x13
+{
+    rd = (rs1 >> op2024) & 1;
+}
+
+# 0110100 rs2 rs1 001 rd 0110011
+:binv rd, rs1, rs2      is op2531=0x34 & rs2 & rs1 & op1214=0x1 & rd & op0006=0x33
+{
+    rd = rs1 ^ (1:$(XLEN) << (rs2 & (($(XLEN) << 3) - 1)));
+}
+
+# 0110100 shamt rs1 001 rd 0010011
+:binvi rd, rs1, op2024  is op2531=0x34 & op2024 & rs1 & op1214=0x1 & rd & op0006=0x13
+{
+    rd = rs1 ^ (1:$(XLEN) << op2024);
+}
+
+# 0010100 rs2 rs1 001 rd 0110011
+:bset rd, rs1, rs2      is op2531=0x14 & rs2 & rs1 & op1214=0x1 & rd & op0006=0x33
+{
+    rd = rs1 | (1:$(XLEN) << (rs2 & (($(XLEN) << 3) - 1)));
+}
+
+# 0010100 shamt rs1 001 rd 0010011
+:bseti rd, rs1, op2024  is op2531=0x14 & op2024 & rs1 & op1214=0x1 & rd & op0006=0x13
+{
+    rd = rs1 | (1:$(XLEN) << op2024);
+}
+
+# ==============================================================================
+# Zbkb Instructions
+# ==============================================================================
+
+# 0110000 rs2 rs1 001 rd 0110011
+:rol rd, rs1, rs2       is op2531=0x30 & rs2 & rs1 & op1214=0x1 & rd & op0006=0x33
+{
+    rd = (rs1 << (rs2 & (($(XLEN) << 3) - 1))) |
+         (rs1 >> (($(XLEN) << 3) - (rs2 & (($(XLEN) << 3) - 1))));
+}
+
+# 0110000 rs2 rs1 101 rd 0110011
+:ror rd, rs1, rs2       is op2531=0x30 & rs2 & rs1 & op1214=0x5 & rd & op0006=0x33
+{
+    rd = (rs1 >> (rs2 & (($(XLEN) << 3) - 1))) |
+         (rs1 << (($(XLEN) << 3) - (rs2 & (($(XLEN) << 3) - 1))));
+}
+
+# 0110000 shamt rs1 101 rd 0010011
+:rori rd, rs1, op2024   is op2531=0x30 & op2024 & rs1 & op1214=0x5 & rd & op0006=0x13
+{
+    rd = (rs1 >> op2024) | (rs1 << (($(XLEN) << 3) - op2024));
+}
+
+# 0110100 11000 rs1 101 rd 0010011
+:rev8 rd, rs1           is op2031=0x698 & rs1 & op1214=0x5 & rd & op0006=0x13
+{
+    rd = ((rs1 & 0xFF) << 24) |
+         ((rs1 & 0xFF00) << 8) |
+         ((rs1 >> 8) & 0xFF00) |
+         ((rs1 >> 24) & 0xFF);
+}
+
+# 0000100 rs2 rs1 100 rd 0110011
+:pack rd, rs1, rs2      is op2531=0x4 & rs2 & rs1 & op1214=0x4 & rd & op0006=0x33
+{
+    rd = (zext(rs2:$(HXLEN)) << ($(XLEN) << 2)) | zext(rs1:$(HXLEN));
+}
+
+# 0000100 rs2 rs1 111 rd 0110011
+:packh rd, rs1, rs2     is op2531=0x4 & rs2 & rs1 & op1214=0x7 & rd & op0006=0x33
+{
+    rd = (zext(rs2:1) << 8) | zext(rs1:1);
+}
+
+# 0110100 00111 rs1 101 rd 0010011
+:brev8 rd, rs1          is op2031=0x687 & rs1 & op1214=0x5 & rd & op0006=0x13
+{
+    local tmp = ((rs1 & 0x5555555555555555) << 1) | ((rs1 >> 1) & 0x5555555555555555);
+    tmp =       ((tmp & 0x3333333333333333) << 2) | ((tmp >> 2) & 0x3333333333333333);
+    rd =        ((tmp & 0x0F0F0F0F0F0F0F0F) << 4) | ((tmp >> 4) & 0x0F0F0F0F0F0F0F0F);
+}
+
+# ==============================================================================
+# Unimpl Instructions
+# ==============================================================================
+
+# 0110000 00000 rs1 001 rd 0010011
+:clz rd, rs1            is op2531=0x30 & op2024=0x0 & rs1 & op1214=0x1 & rd & op0006=0x13 unimpl
+
+# 0110000 00001 rs1 001 rd 0010011
+:ctz rd, rs1            is op2531=0x30 & op2024=0x1 & rs1 & op1214=0x1 & rd & op0006=0x13 unimpl
+
+# 0000101 rs2 rs1 001 rd 0110011
+:clmul rd, rs1, rs2     is op2531=0x5 & rs2 & rs1 & op1214=0x1 & rd & op0006=0x33 unimpl
+
+# 0000101 rs2 rs1 011 rd 0110011
+:clmulh rd, rs1, rs2    is op2531=0x5 & rs2 & rs1 & op1214=0x3 & rd & op0006=0x33 unimpl
+
+# 0000101 rs2 rs1 010 rd 0110011
+:clmulr rd, rs1, rs2    is op2531=0x5 & rs2 & rs1 & op1214=0x2 & rd & op0006=0x33 unimpl
+
+# 0000100 01111 rs1 001 rd 0010011
+:zip rd, rs1            is op2531=0x4 & op2024=0xf & rs1 & op1214=0x1 & rd & op0006=0x13 unimpl
+
+# 0000100 01111 rs1 101 rd 0010011
+:unzip rd, rs1          is op2531=0x4 & op2024=0xf & rs1 & op1214=0x5 & rd & op0006=0x13 unimpl
+
+# 0010100 rs2 rs1 010 rd 0110011
+:xperm4 rd, rs1, rs2    is op2531=0x14 & rs2 & rs1 & op1214=0x2 & rd & op0006=0x33 unimpl
+
+# 0010100 rs2 rs1 100 rd 0110011
+:xperm8 rd, rs1, rs2    is op2531=0x14 & rs2 & rs1 & op1214=0x4 & rd & op0006=0x33 unimpl
diff --git a/Ghidra/Processors/RISCV/data/languages/riscv.rv64b.sinc b/Ghidra/Processors/RISCV/data/languages/riscv.rv64b.sinc
index fe73f244e0..c92a1e04e5 100644
--- a/Ghidra/Processors/RISCV/data/languages/riscv.rv64b.sinc
+++ b/Ghidra/Processors/RISCV/data/languages/riscv.rv64b.sinc
@@ -1,109 +1,136 @@
-# RV64 Bitmanip Extension
-
-#TODO  fix op2031
-:addiwu rd, rs1, op2031 is op0006=0x1b & op1214=0x4 & rd & op2031 & rs1 unimpl
-
-:add.unshflw rd, rs1, rs2 is op0006=0x3b & op1214=0x0 & op2531=0x4 & rd & rs1 & rs2 unimpl
-
-:addwu rd, rs1, rs2 is op0006=0x3b & op1214=0x0 & op2531=0x5 & rd & rs1 & rs2 unimpl
-
-:bdepw rd, rs1, rs2 is op0006=0x3b & op1214=0x6 & op2531=0x24 & rd & rs1 & rs2 unimpl
-
-:bextw rd, rs1, rs2 is op0006=0x3b & op1214=0x6 & op2531=0x4 & rd & rs1 & rs2 unimpl
-
-:bfpw rd, rs1, rs2 is op0006=0x3b & op1214=0x7 & op2531=0x24 & rd & rs1 & rs2 unimpl
-
-:bmatflip rd, rs1 is op0006=0x13 & op1214=0x1 & op2024=0x3 & op2531=0x30 & rd & rs1 unimpl
-
-:bmator rd, rs1, rs2 is op0006=0x33 & op1214=0x3 & op2531=0x4 & rd & rs1 & rs2 unimpl
-
-:bmatxor rd, rs1, rs2 is op0006=0x33 & op1214=0x3 & op2531=0x24 & rd & rs1 & rs2 unimpl
-
-:clmulhw rd, rs1, rs2 is op0006=0x3b & op1214=0x3 & op2531=0x5 & rd & rs1 & rs2 unimpl
-
-:clmulrw rd, rs1, rs2 is op0006=0x3b & op1214=0x2 & op2531=0x5 & rd & rs1 & rs2 unimpl
-
-:clmulw  rd, rs1, rs2 is op0006=0x3b & op1214=0x1 & op2531=0x5 & rd & rs1 & rs2 unimpl
-
-:clzw rd, rs1 is op0006=0x1b & op1214=0x1 & op2024=0x0 & op2531=0x30 & rd & rs1 unimpl
-
-:crc32.d rd, rs1 is op0006=0x13 & op1214=0x1 & op2024=0x13 & op2531=0x30 & rd & rs1 unimpl
-
-:crc32c.d rd, rs1 is op0006=0x13 & op1214=0x1 & op2024=0x1b & op2531=0x30 & rd & rs1 unimpl
-
-:ctzw rd, rs1 is op0006=0x1b & op1214=0x1 & op2024=0x1 & op2531=0x30 & rd & rs1 unimpl
-
-:fslw  rd, rs1, rs3, rs2 is op0006=0x3b & op1214=0x1 & op2526=0x2 & rd & rs1 & rs2 & rs3 unimpl
-
-#TODO fix op2024
-:fsriw rd, rs1, rs3, op2024 is op0006=0x1b & op1214=0x5 & op2526=0x2 & op2024 & rd & rs1 & rs3 unimpl
-
-:fsrw  rd, rs1, rs3, rs2 is op0006=0x3b & op1214=0x5 & op2526=0x2 & rd & rs1 & rs2 & rs3 unimpl
-
-#TODO  fix op2024
-:gorciw rd, rs1, op2024 is op0006=0x1b & op1214=0x5 & op2531=0x14 & op2024 & rd & rs1 unimpl
-
-:gorcw  rd, rs1, rs2 is op0006=0x3b & op1214=0x5 & op2531=0x14 & rd & rs1 & rs2 unimpl
-
-#TODO  fix op2024
-:greviw rd, rs1, op2024 is op0006=0x1b & op1214=0x5 & op2531=0x34 & op2024 & rd & rs1 unimpl
-
-:grevw  rd, rs1, rs2 is op0006=0x3b & op1214=0x5 & op2531=0x34 & rd & rs1 & rs2 unimpl
-
-:packuw rd, rs1, rs2 is op0006=0x3b & op1214=0x4 & op2531=0x24 & rd & rs1 & rs2 unimpl
-
-:zext.h  rd, rs1, rs2 is op0006=0x3b & op1214=0x4 & op2531=0x4 & rd & rs1 & rs2 unimpl
-
-:cpopw rd, rs1 is op0006=0x1b & op1214=0x1 & op2024=0x2 & op2531=0x30 & rd & rs1 unimpl
-
-:rolw  rd, rs1, rs2 is op0006=0x3b & op1214=0x1 & op2531=0x30 & rd & rs1 & rs2 unimpl
-
-#TODO  fix op2024
-:roriw rd, rs1, op2024 is op0006=0x1b & op1214=0x5 & op2531=0x30 & op2024 & rd & rs1 unimpl
-
-:rorw  rd, rs1, rs2 is op0006=0x3b & op1214=0x5 & op2531=0x30 & rd & rs1 & rs2 unimpl
-
-#TODO  fix op2024
-:sbclriw rd, rs1, op2024 is op0006=0x1b & op1214=0x1 & op2531=0x24 & op2024 & rd & rs1 unimpl
-
-:sbclrw  rd, rs1, rs2 is op0006=0x3b & op1214=0x1 & op2531=0x24 & rd & rs1 & rs2 unimpl
-
-:sbextw  rd, rs1, rs2 is op0006=0x3b & op1214=0x5 & op2531=0x24 & rd & rs1 & rs2 unimpl
-
-#TODO  fix op2024
-:sbinviw rd, rs1, op2024 is op0006=0x1b & op1214=0x1 & op2531=0x34 & op2024 & rd & rs1 unimpl
-
-:sbinvw  rd, rs1, rs2 is op0006=0x3b & op1214=0x1 & op2531=0x34 & rd & rs1 & rs2 unimpl
-
-#TODO  fix op2024
-:sbsetiw rd, rs1, op2024 is op0006=0x1b & op1214=0x1 & op2531=0x14 & op2024 & rd & rs1 unimpl
-
-:sbsetw  rd, rs1, rs2 is op0006=0x3b & op1214=0x1 & op2531=0x14 & rd & rs1 & rs2 unimpl
-
-:sh1add.uw rd, rs1, rs2 is op0006=0x3b & op1214=0x2 & op2531=0x10 & rd & rs1 & rs2 unimpl
-
-:sh2add.uw rd, rs1, rs2 is op0006=0x3b & op1214=0x4 & op2531=0x10 & rd & rs1 & rs2 unimpl
-
-:sh3add.uw rd, rs1, rs2 is op0006=0x3b & op1214=0x6 & op2531=0x10 & rd & rs1 & rs2 unimpl
-
-:shflw    rd, rs1, rs2 is op0006=0x3b & op1214=0x1 & op2531=0x4 & rd & rs1 & rs2 unimpl
-
-#TODO  fix op2026
-:slliu.w rd, rs1, op2026 is op0006=0x1b & op1214=0x1 & op2731=0x1 & op2026 & rd & rs1 unimpl
-
-#TODO  fix op2024
-:sloiw rd, rs1, op2024 is op0006=0x1b & op1214=0x1 & op2531=0x10 & op2024 & rd & rs1 unimpl
-
-:slow  rd, rs1, rs2 is op0006=0x3b & op1214=0x1 & op2531=0x10 & rd & rs1 & rs2 unimpl
-
-#TODO  fix op2024
-:sroiw rd, rs1, op2024 is op0006=0x1b & op1214=0x5 & op2531=0x10 & op2024 & rd & rs1 unimpl
-
-:srow  rd, rs1, rs2 is op0006=0x3b & op1214=0x5 & op2531=0x10 & rd & rs1 & rs2 unimpl
-
-:subu.w rd, rs1, rs2 is op0006=0x3b & op1214=0x0 & op2531=0x24 & rd & rs1 & rs2 unimpl
-
-:subwu rd, rs1, rs2 is op0006=0x3b & op1214=0x0 & op2531=0x25 & rd & rs1 & rs2 unimpl
-
-:unshflw  rd, rs1, rs2 is op0006=0x3b & op1214=0x5 & op2531=0x4 & rd & rs1 & rs2 unimpl
-
+# RISC-V Zc & Zb Extensions (RV64 Specifics)
+# NOTE: v1.0.0
+# Reference: https://docs.riscv.org/reference/isa/unpriv/b-st-ext.html
+
+# ==============================================================================
+# Zba Instructions
+# ==============================================================================
+
+# 0000100 rs2 rs1 000 rd 0111011
+:add.uw rd, rs1, rs2    is op2531=0x4 & rs2 & rs1 & op1214=0x0 & rd & op0006=0x3b
+{
+    rd = rs2 + zext(rs1:4);
+}
+
+# 0010000 rs2 rs1 010 rd 0111011
+:sh1add.uw rd, rs1, rs2 is op2531=0x10 & rs2 & rs1 & op1214=0x2 & rd & op0006=0x3b
+{
+    rd = rs2 + (zext(rs1:4) << 1);
+}
+
+# 0010000 rs2 rs1 100 rd 0111011
+:sh2add.uw rd, rs1, rs2 is op2531=0x10 & rs2 & rs1 & op1214=0x4 & rd & op0006=0x3b
+{
+    rd = rs2 + (zext(rs1:4) << 2);
+}
+
+# 0010000 rs2 rs1 110 rd 0111011
+:sh3add.uw rd, rs1, rs2 is op2531=0x10 & rs2 & rs1 & op1214=0x6 & rd & op0006=0x3b
+{
+    rd = rs2 + (zext(rs1:4) << 3);
+}
+
+# 000010 shamt rs1 001 rd 0011011
+:slli.uw rd, rs1, op2025 is op2631=0x2 & op2025 & rs1 & op1214=0x1 & rd & op0006=0x1b
+{
+    rd = zext(rs1:4) << op2025;
+}
+
+# ==============================================================================
+# Zbb Instructions
+# ==============================================================================
+
+# 0110000 00010 rs1 001 rd 0011011
+:cpopw rd, rs1          is op2531=0x30 & op2024=0x2 & rs1 & op1214=0x1 & rd & op0006=0x1b
+{
+    local tmp:4 = popcount(rs1:4);
+    rd = zext(tmp);
+}
+
+# 0000100 00000 rs1 100 rd 0111011
+:zext.h rd, rs1         is op2531=0x4 & op2024=0x0 & rs1 & op1214=0x4 & rd & op0006=0x3b
+{
+    rd = zext(rs1:2);
+}
+
+# ==============================================================================
+# Zbs Instructions
+# ==============================================================================
+
+# 010010 shamt rs1 001 rd 0010011
+:bclri rd, rs1, op2025  is op2631=0x12 & op2025 & rs1 & op1214=0x1 & rd & op0006=0x13
+{
+    rd = rs1 & ~(1:$(XLEN) << op2025);
+}
+
+# 010010 shamt rs1 101 rd 0010011
+:bexti rd, rs1, op2025  is op2531=0x12 & op2025 & rs1 & op1214=0x5 & rd & op0006=0x13
+{
+    rd = (rs1 >> op2025) & 1;
+}
+
+# 011010 shamt rs1 001 rd 0010011
+:binvi rd, rs1, op2025  is op2531=0x1a & op2025 & rs1 & op1214=0x1 & rd & op0006=0x13
+{
+    rd = rs1 ^ (1:$(XLEN) << op2025);
+}
+
+# 001010 shamt rs1 001 rd 0010011
+:bseti rd, rs1, op2025  is op2531=0xa & op2025 & rs1 & op1214=0x1 & rd & op0006=0x13
+{
+    rd = rs1 | (1:$(XLEN) << op2025);
+}
+
+# ==============================================================================
+# Zbkb Instructions
+# ==============================================================================
+
+# 011010 111000 rs1 101 rd 0010011
+:rev8 rd, rs1           is op2031=0x6b8 & rs1 & op1214=0x5 & rd & op0006=0x13
+{
+    rd = ((rs1 & 0xFF) << 56) | ((rs1 & 0xFF00) << 40) |
+         ((rs1 & 0xFF0000) << 24) | ((rs1 & 0xFF000000) << 8) |
+         ((rs1 >> 8) & 0xFF000000) | ((rs1 >> 24) & 0xFF0000) |
+         ((rs1 >> 40) & 0xFF00) | ((rs1 >> 56) & 0xFF);
+}
+
+# 0000100 rs2 rs1 100 rd 0111011
+:packw rd, rs1, rs2     is op2531=0x4 & rs2 & rs1 & op1214=0x4 & rd & op0006=0x3b
+{
+    local tmp:4 = (zext(rs2:2) << 16) | zext(rs1:2);
+    rd = sext(tmp);
+}
+
+# 0110000 rs2 rs1 001 rd 0111011
+:rolw rd, rs1, rs2      is op2531=0x30 & rs2 & rs1 & op1214=0x1 & rd & op0006=0x3b
+{
+    rd = sext((rs1:4 << (rs2 & 0x1F)) | (rs1:4 >> (32 - (rs2 & 0x1F))));
+}
+
+# 0110000 rs2 rs1 101 rd 0111011
+:rorw rd, rs1, rs2      is op2531=0x30 & rs2 & rs1 & op1214=0x5 & rd & op0006=0x3b
+{
+    rd = sext( (rs1:4 >> (rs2 & 0x1F)) | (rs1:4 << (32 - (rs2 & 0x1F))) );
+}
+
+# 011000 shamt rs1 101 rd 0010011
+:rori rd, rs1, op2025   is op2631=0x18 & op2025 & rs1 & op1214=0x5 & rd & op0006=0x13
+{
+    rd = (rs1 >> op2025) | (rs1 << (($(XLEN) << 3) - op2025));
+}
+
+# 0110000 shamt rs1 101 rd 0011011
+:roriw rd, rs1, op2024  is op2531=0x30 & op2024 & rs1 & op1214=0x5 & rd & op0006=0x1b
+{
+    rd = sext((rs1:4 >> op2024) | (rs1:4 << (32 - op2024)));
+}
+
+# ==============================================================================
+# Unimpl Instructions
+# ==============================================================================
+
+# 0110000 00001 rs1 001 rd 0011011
+:ctzw rd, rs1           is op2531=0x30 & op2024=0x1 & rs1 & op1214=0x1 & rd & op0006=0x1b unimpl
+
+# 0110000 00000 rs1 001 rd 0011011
+:clzw rd, rs1           is op2531=0x30 & op2024=0x0 & rs1 & op1214=0x1 & rd & op0006=0x1b unimpl
-- 
2.45.1

